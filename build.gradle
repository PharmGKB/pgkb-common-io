buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath 'biz.aQute.bnd:biz.aQute.bnd.gradle:5.3.0'
  }
}

plugins {
  id 'java-library'
  id 'jacoco'
  id 'signing'
  id 'maven-publish'
  id "io.github.gradle-nexus.publish-plugin" version "1.0.0"
}
apply plugin: 'biz.aQute.bnd.builder'


def getVersionName() {
  def errOut = new ByteArrayOutputStream()
  try {
    def stdOut = new ByteArrayOutputStream()
    exec {
      commandLine 'git', 'describe', '--tags'
      standardOutput = stdOut
      errorOutput = errOut
    }
    // strip of leading "v"
    return stdOut.toString().trim().substring(1)
  } catch (ignore) {
    logger.info('Error running git describe (' + errOut.toString().trim() + '), defaulting to 0.0.0')
    return '0.0.0'
  }
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8
project.group = 'org.pharmgkb'
project.version = getVersionName()

ext {
  baseName = 'pgkb-common-io'
  moduleName = 'org.pharmgkb.common.io'
  description = 'Common IO code shared among all PharmGKB projects.'
  url = 'https://github.com/PharmGKB/' + project.ext.baseName + '.git'
  scm = 'scm:git@github.com:PharmGKB/' + project.ext.baseName + '.git'
  orgName = 'PharmGKB'
  orgUrl = 'https://www.pharmgkb.org'
  // grab properties from environment for CI
  sonatypeUser = project.hasProperty('ossrhUsername') ? ossrhUsername : System.getenv('OSSRH_USERNAME')
  sonatypePwd  = project.hasProperty('ossrhPassword') ? ossrhPassword : System.getenv('OSSRH_PASSWORD')
  sonatypeStagingProfileId = project.hasProperty('ossrhStagingProfileId') ? ossrhPassword : System.getenv('OSSRH_STAGING_PROFILE_ID')
}

// cannot modify signing directly, have to do it like this
// reference: https://getstream.io/blog/publishing-libraries-to-mavencentral-2021/
ext['signing.keyId'] = ''
ext['signing.password'] = ''
ext['signing.secretKeyRingFile'] = ''
if (project.hasProperty('gpg.keyId')) {
  ext['signing.keyId'] =  project.getProperty('gpg.keyId')
  ext['signing.password'] = project.getProperty('gpg.password')
  ext['signing.secretKeyRingFile'] =  project.getProperty('gpg.secretKeyRingFile')
} else if (System.getenv('SIGNING_KEY_ID') != null) {
  ext['signing.keyId'] =  System.getenv('SIGNING_KEY_ID')
  ext['signing.password'] = System.getenv('SIGNING_PASSWORD')
  ext['signing.secretKeyRingFile'] =  System.getenv('SIGNING_SECRET_KEY_RING_FILE')
}


repositories {
  jcenter()
}

dependencies {
  api group: 'com.google.api-client', name: 'google-api-client', version: '1.30.2'
  api group: 'com.google.http-client', name: 'google-http-client-gson', version: '1.31.0'
  api group: 'com.google.gdata', name: 'core', version: '1.47.1'
  implementation group: 'commons-cli', name: 'commons-cli', version: '1.4'
  implementation group: 'commons-io', name: 'commons-io', version: '2.8.0'

  implementation group: 'com.google.guava', name: 'guava', version: '30.1.1-jre'
  implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.12.0'
  implementation group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.13'
  implementation group: 'org.checkerframework', name: 'checker-qual', version: '3.12.0'
  implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.28'

  testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: '5.7.1'
  testImplementation group: 'org.bouncycastle', name: 'bcprov-jdk15on', version: '1.68'
  testImplementation group: 'org.bouncycastle', name: 'bcpkix-jdk15on', version: '1.68'
}

apply from: 'pgkb-build.gradle'
